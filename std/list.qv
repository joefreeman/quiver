
list<t> :: Nil | Cons[t, &];

math = %"math",

length' = #<t>[list<t>, int] {
  | ~> =[Nil, x] => x
  | ~> =[Cons[_, rest], x] => math.add[x, 1] ~> &[rest, ~]
},

reverse' = #<t>[list<t>, list<t>] {
  | ~> =[Nil, xs] => xs
  | ~> =[Cons[head, tail], xs] => Cons[head, xs] ~> &[tail, ~]
},

concat' = #<t>[list<t>, list<t>, list<t>] {
  | ~> =[Nil, a, b] => [b, a] ~> reverse'
  | ~> =[Cons[head, tail], a, b] => Cons[head, b] ~> &[tail, a, ~]
},

map' = #<t, u>[list<t>, #t -> u, list<u>] {
  | ~> =[Nil, _, acc] => [acc, Nil] ~> reverse'
  | ~> =[Cons[head, tail], f, acc] => {
    head ~> f ~> Cons[~, acc] ~> &[tail, f, ~]
  }
},

filter' = #<t>[list<t>, #t -> (t | []), list<t>] {
  | ~> =[Nil, _, acc] => [acc, Nil] ~> reverse'
  | ~> =[Cons[head, tail], pred, acc], head ~> pred =>
    [tail, pred, Cons[head, acc]] ~> &
  | ~> =[Cons[_, tail], pred, acc] => &[tail, pred, acc]
},

take' = #<t>[list<t>, int, list<t>] {
  | ~> =[Nil, _, acc] => [acc, Nil] ~> reverse'
  | ~> =[_, 0, acc] => [acc, Nil] ~> reverse'
  | ~> =[Cons[head, tail], n, acc] => {
    math.sub[n, 1] ~> &[tail, ~, Cons[head, acc]]
  }
},

[
  new: #{ Nil },

  head: #<t>list<t> {
    ~> =Cons[head, _] => head
  },

  tail: #<t>list<t> {
    ~> =Cons[_, tail] => tail
  },

  empty?: #<t>list<t> { ~> =Nil => Ok },

  prepend: #<t>[list<t>, t] {
    ~> =[xs, x] => Cons[x, xs]
  },

  append: #<t>[list<t>, t] {
    ~> =[xs, x] => {
      reversed = [xs, Nil] ~> reverse',
      [Cons[x, reversed], Nil] ~> reverse'
    }
  },

  concat: #<t>[list<t>, list<t>] {
    ~> =[xs, ys] => concat'[xs, ys, Nil]
  },

  length: #<t>list<t> {
    ~> =xs => [xs, 0] ~> length'
  },

  reverse: #<t>list<t> {
    ~> =xs => [xs, Nil] ~> reverse'
  },

  at: #<t>[list<t>, int] {
    | ~> =[Nil, _] => []
    | ~> =[Cons[h, _], 0] => h
    | ~> =[Cons[_, t], n] => math.sub[n, 1] ~> &[t, ~]
  },

  map: #<t, u>[list<t>, #t -> u] {
    ~> =[xs, f] => map'[xs, f, Nil]
  },

  filter: #<t>[list<t>, #t -> (t | [])] {
    ~> =[xs, f] => filter'[xs, f, Nil]
  },

  drop: #<t>[list<t>, int] {
    | ~> =[Nil, _] => Nil
    | ~> =[xs, 0] => xs
    | ~> =[Cons[_, tail], n] =>
      math.sub[n, 1] ~> &[tail, ~]
  },

  take: #<t>[list<t>, int] {
    ~> =[xs, x] => take'[xs, x, Nil]
  },

  contains?: #<t>[list<t>, t] {
    | ~> =[Nil, _] => []
    | ~> =[Cons[^value, _], ^value] => Ok
    | ~> =[Cons[_, tail], value] => &[tail, value]
  },

  all?: #<t>[list<t>, #t -> (t | [])] {
    | ~> =[Nil, _] => Ok
    | ~> =[Cons[head, tail], pred], head ~> pred => &[tail, pred]
    | ~> =[Cons[_, _], _] => []
  },

  any?: #<t>[list<t>, #t -> (t | [])] {
    | ~> =[Nil, _] => []
    | ~> =[Cons[head, tail], pred], head ~> pred => Ok
    | ~> =[Cons[_, tail], pred] => &[tail, pred]
  }
]
